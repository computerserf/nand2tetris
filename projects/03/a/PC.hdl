// This file contains the implementation of a 16-bit counter

// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    
    Inc16(in=fout, out=plus1);
    
    Mux16(a=fout, b=plus1, out=out1, sel=inc);
    Mux16(a=out1, b=in, out=out2, sel=load);
    Mux16(a=out2, b=false, out=fin, sel=reset);
    
    Register(in=fin, load=true, out=out, out=fout);
}
