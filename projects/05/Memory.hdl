// This file contains an implementation of the memory chhip.

// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Check whether upper 4 bits is 4, 5, 6 or invalid
	DMux8Way(in=true, sel=address[12..14], a=ram0, b=ram1, c=ram2, d=ram3, e=screen0, f=screen1, g=keyboard, h=ignore);
	
	Or8Way(in[0]=ram0, in[1]=ram1, in[2]=ram2, in[3]=ram3, in[4..7]=false, out=ram);
	Or8Way(in[0]=screen0, in[1]=screen1, in[2..7]=false, out=screen);
	
	// Compute outputs
	And(a=ram, b=load, out=loadram);
	RAM16K(in=in, load=loadram, address=address[0..13], out=ramout);
	
	And(a=screen, b=load, out=loadscreen);
	Screen(in=in, load=loadscreen, address=address[0..12], out=screenout);
	
	Keyboard(out=keyboardout);
	
	// Select the correct output
	Mux8Way16(sel[0]=ram, sel[1]=screen, sel[2]=keyboard, a=false, b=ramout, c=screenout, d=false, e=keyboardout, f=false, g=false, h=false, out=out);

}